plugins {
    id 'java'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.9' apply false
    id 'me.champeau.jmh' version '0.7.2' apply false
}

// group, version, and description are managed in gradle.properties
description = 'curve'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url = uri("https://packages.confluent.io/maven/")
        }
    }
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.9"
        }
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // Test
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.mockito:mockito-core'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/config/**',
                        '**/configuration/**',
                        '**/autoconfigure/**',
                        '**/*Application.*',
                        '**/*Config.*',
                        '**/*Properties.*'
                ])
            }))
        }
    }

    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.70
                }
                excludes = [
                        '*.config.*',
                        '*.configuration.*',
                        '*.autoconfigure.*',
                        '*Application',
                        '*Config',
                        '*Properties',
                        // Kafka
                        '*KafkaEventProducer',
                        // Spring Serializers (통합 테스트로 검증)
                        '*EventSerializer',
                        // Spring Context Providers (Spring 통합 필요)
                        '*ContextProvider',
                        '*.context.*ContextDecorator',
                        '*ContextAwareTaskDecorator',
                        // Spring Outbox (데이터베이스 통합 필요)
                        '*OutboxEventPublisher*',
                        '*OutboxEventSaver',
                        '*OutboxEventJpaEntity',
                        '*OutboxEventRepository*',
                        '*JdbcOutboxEventRepository*',
                        '*JpaOutboxEventRepositoryAdapter',
                        // Spring PII Jackson (Jackson 통합 필요)
                        '*PiiModule',
                        '*PiiPropertyWriter',
                        '*PiiBeanSerializerModifier',
                        '*PiiProcessor*',
                        // Spring Publisher (추상 클래스)
                        '*AbstractEventPublisher',
                        // Test utilities
                        '*MockEventProducer',
                        // Validators (SPEL 통합 필요)
                        '*SpelExpressionValidator',
                        // Default types
                        '*DefaultEventType',
                        // Metrics (Micrometer 통합 필요)
                        '*MicrometerCurveMetricsCollector'
                ]
            }
        }
    }

    tasks.withType(Javadoc).configureEach {
        def opts = options as StandardJavadocDocletOptions

        opts.encoding = 'UTF-8'
        opts.charSet = 'UTF-8'
        opts.docEncoding = 'UTF-8'

        opts.addStringOption('Xdoclint:none', '-quiet')

        if (JavaVersion.current().isJava9Compatible()) {
            opts.addBooleanOption('html5', true)
        }

        opts.links(
                'https://docs.oracle.com/en/java/javase/17/docs/api/',
                'https://docs.spring.io/spring-framework/docs/current/javadoc-api/',
                'https://docs.spring.io/spring-boot/docs/current/api/'
        )
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    tasks.register('sourcesJar', Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}
